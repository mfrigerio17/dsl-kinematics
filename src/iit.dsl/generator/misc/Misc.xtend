package iit.dsl.generator.misc

import iit.dsl.kinDsl.Robot

import iit.dsl.generator.Common
import iit.dsl.generator.Utilities
import iit.dsl.kinDsl.PrismaticJoint
import iit.dsl.kinDsl.RevoluteJoint
import iit.dsl.kinDsl.AbstractLink
import iit.dsl.kinDsl.InertiaParams

class Misc {
    static Misc singleton = null

    def static Misc getInstance() {
        if(singleton == null) {
            singleton = new Misc()
        }
        return singleton
    }



    private extension Common common = new Common()

    private new() { }

    /**
     * Generates and xml URDF description of the robot, as specified in the ROS documentation.
     * The URDF file format requires the inertia tensor to be expressed in a reference frame
     * with origin in the center of mass. Therefore this generator takes the inertia tensor
     * expressed in the link-default-frame and translates it to the COM before printing the values.
     */
    def public URDF_ROS_model(Robot robot) '''
    <robot name="«robot.name»">
    «FOR link : robot.abstractLinks»
        «val inertia_lf = link.linkFrameInertiaParams /*inertia params expressed in the default link frame*/»
        «val com = inertia_lf.com»
        «val inertia = /*inertia params expressed in the frame centered in the COM*/
             Utilities::rototranslate(inertia_lf, com.x.asFloat,com.y.asFloat,com.z.asFloat,0,0,0,false)»
        <link name="«link.name»">
            <inertial>
                <origin xyz="«inertia_lf.com.x.str» «inertia_lf.com.y.str» «inertia_lf.com.z.str»"/>
                <mass value="«inertia.mass»"/>
                <inertia ixx="«inertia.ix»" iyy="«inertia.iy»" izz="«inertia.iz»" ixy="«inertia.ixy»" ixz="«inertia.ixz»" iyz="«inertia.iyz»"/>
            </inertial>
        </link>
    «ENDFOR»
    «FOR joint : robot.joints»
        «val frame = joint.refFrame»
        <joint name="«joint.name»" type="«joint.typeString»">
            <origin xyz="«frame.translation.listCoordinates()»" rpy="«frame.rotation.x.asFloat» «frame.rotation.y.asFloat» «frame.rotation.z.asFloat»"/>
            <parent link="«joint.predecessorLink.name»"/>
            <child  link="«joint.successorLink.name»"/>
            <limit effort="30" velocity="1.0"/>
            <axis xyz="0 0 1"/>
        </joint>
    «ENDFOR»
    </robot>
    '''

    /**
     * Generates the content of an SD/FAST input file representing the given robot
     */
    def SDFAST_model(Robot robot) '''
        #  This is an SD/FAST input file describing the robot «robot.name»
        #
        #  This file has been automatically generated by the robotics code generator
        #   developed by Marco Frigerio at the Italian Institute of Technology.
        #      November 2012

        language = c
        gravity = 0 0 -9.8

        «IF robot.base.floating»
            # Floating base is not supported yet!!! #
        «ELSE»
            «val iparams = sdfast_inertiaParams(robot.base)»
            body = «robot.base.name»
              inb = $ground
              joint = weld
              mass = «iparams.mass»
              inertia =
                «iparams.ix» «iparams.ixy» «iparams.ixz»
                «iparams.ixy» «iparams.iy» «iparams.iyz»
                «iparams.ixz» «iparams.iyz» «iparams.iz»
              bodytojoint = 0 0 0
        «ENDIF»

        «FOR link : robot.links»
            «val iparams = sdfast_inertiaParams(link)»
            body = «link.name»
              inb = «link.parent.name»
              joint = «sdfast_jointType(link.connectingJoint)»
              pin = 0 0 1
              mass = «iparams.mass»
              inertia =
                «iparams.ix» «iparams.ixy» «iparams.ixz»
                «iparams.ixy» «iparams.iy» «iparams.iyz»
                «iparams.ixz» «iparams.iyz» «iparams.iz»
              bodytojoint = «sdfast_bodyCOMToJointVector(link)»
              inbtojoint = «sdfast_parentBodyToJointVector(link)»

        «ENDFOR»
    '''
    def private dispatch sdfast_jointType(PrismaticJoint j) '''slider'''
    def private dispatch sdfast_jointType(RevoluteJoint  j) '''pin'''
    def private InertiaParams sdfast_inertiaParams(AbstractLink body) {
        val iparams = body.linkFrameInertiaParams

        val iparams_new = /*inertia params expressed in the frame centered in the COM*/
                Utilities::rototranslate(
                iparams,
                iparams.com.x.asFloat,
                iparams.com.y.asFloat,
                iparams.com.z.asFloat,
                0,0,0,false //TODO compute the angles
            )

        return iparams_new
    }
    def private sdfast_bodyCOMToJointVector(AbstractLink link) {
        val x = Utilities::invert(link.inertiaParams.com.x.asFloat)
        val y = Utilities::invert(link.inertiaParams.com.y.asFloat)
        val z = Utilities::invert(link.inertiaParams.com.z.asFloat)
        return '''«x» «y» «z»'''
    }
    def private sdfast_parentBodyToJointVector(AbstractLink body) {
        return body.connectingJoint.refFrame.translation.listCoordinates
    }


}
